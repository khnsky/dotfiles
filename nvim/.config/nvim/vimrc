if has('multi_byte')
    " in most cases if $LANG is not set vim falls back to latin1
    " https://sanctum.geek.nz/cgit/dotfiles.git/tree/vim/vimrc#n75
    if &encoding ==# 'latin1' && !exists('$LANG')
        set encoding=utf-8
    endif
    scriptencoding utf-8
endif

" vint: -ProhibitSetNoCompatible
if &compatible                          " avoid side effects if already set
    set nocompatible                    " turn vi compatibility off
endif

silent! while 0
    set nocompatible
silent! endwhile
" vint: +ProhibitSetNoCompatible

if 1                                    " has +eval
    filetype indent plugin on           " enable file type plugins and indent

    augroup vimrc
        autocmd!
    augroup END
endif

if has('syntax')
    if !exists('g:syntax_on')
        syntax enable                   " load syntax highlight once
    endif

    autocmd vimrc VimEnter * colorscheme mine
    " for some reason vim has no colors
    if has('nvim') && $COLORTERM =~# '^\(truecolor\|24bit\)$'
        set termguicolors
    endif

    set synmaxcol=300                   " stop searching for syntax items after
endif

" reload buffer if file detected to have changed
" write file when changing buffers
" disable backup, swap and viminfo files
set autoread autowriteall nobackup noswapfile nowritebackup viminfo=

if has('persistent_undo')               " semi-persistent undo history
    set undofile undodir=/tmp,.         " TODO: not sure about security of this
endif

if has('path_extra')                    " see :h file-searching
    set path=.,,**                      " search down when using 'path' (:find)
    set tags=./tags; tags+=tags         " search for tags in current dir and up in dir of current file
    if has('emacs_tags')
        set tags+=./TAGS; tags+=TAGS    
    endif
endif

set tagcase=match                       " match tags case not following ignorecase

" case insensitive search by default unless uppercase letter used, highlight
" all matches, serach incrementally
set ignorecase smartcase hlsearch incsearch

if exists('+inccommand')
    set inccommand=split
endif

" use previous line indent
" sane values for c/c++ indentation - see :h cinoptions-values
set autoindent cinoptions=N-s,:0,l1,b0,g0,t0,(0,U1,W1s,m1,j1

" insert spaces in place of tabs, round indent to multiple of 'sw'
" use &tabstop for each step of (auto)indent
" use sw at the start of a line, sts else
" insert 4 spaces for tab and represent tab by 4 columns
set expandtab shiftround shiftwidth=0 smarttab tabstop=4
let &softtabstop = &tabstop

set wrap                                " wrap lines too long to display
if has('linebreak')
    " break lines at sensible places and keep indentation when wrapping
    set linebreak breakindent
endif

if has('folding')
    " enable syntax based folding, folds opened in autocmd
    set nofoldenable foldmethod=syntax
endif

" add <:> and =:; to matching pairs, highligth match to the one under cursor
set matchpairs=(:),{:},[:],<:>,=:; showmatch

" include file completion is slow
" show menu even for single completion, show extra information in preview
" window, force user to select match
set complete-=i completeopt=menuone,preview,noselect

set nrformats=bin,hex                   " predictable number inc/decreasing
set backspace=2                         " backspace over everything in insert
set virtualedit=block                   " free movement in block mode

" 1 - don't break lines after one-letter words, break it before if possible
" l - don't break lines that were too long before entering insert
" n - recognize numbered lists (using 'formatlistpat') and indent accordingly
" t and c flags (auto-wrap of text and comments respectively) should be set in
" appropriate ftplugins
set formatoptions+=1ln

" remove comment leader when joining lines
if has#('patch-7.3.0541')
    set formatoptions+=j
endif

" two-spacing sentences all the way
" joinspaces inserts two spaces after '.', '?' or '!' with join command
" joinspaces is already set by deafault but set it anyway for clarity
set cpoptions+=J joinspaces
if has#('patch-8.1.728')
    set formatoptions+=p
endif

" setting spell before spelllang will load spell files twice or something
" toggle and print spell with <leader>s
" fix last spellnig mistake by selecting first suggestion
set nospell spelllang=pl,en_us,en_gb
nnoremap <leader>s :set spell! spell?<cr>
inoremap <C-l> <c-g>u<Esc>[s1z=`]a<c-g>u

" because of using two-spaced sentences spellcapcheck can be more restricive
set spellcapcheck=[.?!]\\%(\ \ \\\|[\\n\\r\\t]\\)

" treat snakeCased and CamelCased words as separate when spell checking
if exists('+spelloptions')
   set spelloptions+=camel
endif

" these defaults are ancient and incredibly c-centric
" clear them here and set appropriately in ftplugins
set comments= commentstring= define= include=

" enable mouse support if available
" `nvi` allows to use mouse in most modes but copy text from remote
" vim instance by using command mode
" use ttymouse=xterm2 or sgr when available to resize windows with mouse
silent! set mouse=nvi ttymouse=xterm2 ttymouse=sgr

" put splits below current window and vertical split to the right
set splitbelow splitright

" jump 10 lines and 5 columns on scrolloff, keep 1 line context on screen
set scrolljump=10 scrolloff=1 sidescrolloff=5

set visualbell                          " use visual bell instead of beeping

" avoid hit-enter prompts, show keys in a current chord, always show
" statusline, show cursor position in statusline
set cmdheight=2 showcmd laststatus=2 ruler

" show dialogs on eg. quitting unsaved, show possible completiions
" complete common then full matches
" ignore implied directories, ignore case in file completion
set confirm wildmenu wildmode=longest:full,full wildignorecase
set wildignore=./,../,**/./,**/../

" all default but use line cursor for command mode
set guicursor=n-v-sm:block,i-c-ci-ve:ver25,r-cr-o:hor20

" use line cursor for insert mode, block cursor for normal in vim
" might not work for one terminal or another
if !has('nvim')
    let &t_SI = "\e[6 q"
    let &t_EI = "\e[2 q"

    autocmd vimrc VimEnter * silent! !print -n "\e[2 q"
endif

" short timeout on key codes
set ttimeout ttimeoutlen=100

if has('langmap') && exists('+langremap')
    " no langmap for results of mappings
    set nolangremap
endif

" break undo on <C-U>
inoremap <C-U> <C-G>u<C-U>

" break undo on newline
if has#('patch-7.3.0489')
    " expand abbreviations
    inoremap <CR> <C-G>u<C-]><CR>
else
    " without this patch it inserts literal ^]
    inoremap <CR> <C-G>u<CR>
endif

let mapleader = '\'
map <Space> <Leader>

" use Q for formatting, there is gQ for Ex
map Q gq
" copy to EOL instead of copying line, more analogous to the rest of operators
map Y y$

" try to keep cursor in the same column when scrolling, moving, jumping
" go to the column  of the mark instead of going to the first non-blank
set nostartofline
map ' `

" clear search highlight in addition to clearing & redrawing screen
if has('nvim')
    nnoremap <C-L> <Cmd>nohl<CR><C-L>
else
    nnoremap <C-L> :nohl<CR><C-L>
endif

" don't expand abbreviations in console with Return
" mapped Return doesn't trigger expansion even if it is mapped to itself
cnoremap <CR> <CR>

nmap <silent> <Leader>z <Plug>ZoomIt

xnoremap < <gv
xnoremap > >gv

map <Leader>h <C-w>h
map <Leader>j <C-w>j
map <Leader>k <C-w>k
map <Leader>l <C-w>l

map <Leader>H <C-w>H
map <Leader>J <C-w>J
map <Leader>K <C-w>K
map <Leader>L <C-w>L

nnoremap <tab>   <c-w>w
nnoremap <s-tab> <c-w>W

nnoremap [q :cprev<cr>zz
nnoremap ]q :cnext<cr>zz
nnoremap [l :lprev<cr>zz
nnoremap ]l :lnext<cr>zz

nnoremap <silent> <leader>c :cclose <bar> lclose<cr>

nnoremap [b :bprev<cr>
nnoremap ]b :bnext<cr>

nnoremap [t :tabp<cr>
nnoremap ]t :tabn<cr>

nnoremap <leader>o o<c-[>
nnoremap <leader>O O<c-[>

" prevent code execution when pasting from system clipboard
" <c-r><c-r> inserts text from register like <c-r> but does so literally
inoremap <c-r>+ <c-r><c-r>+
inoremap <c-r>* <c-r><c-r>*

" <c-w>] and <c-w><c-]> do the same - make so <c-w>] opens tag in vsplit
nnoremap <c-w>] :SD wincmd ]<cr>
" open file under the cursor in a split
nnoremap <c-w>f :SD wincmd f<cr>

" open quickfix list populated with files in current directory
nmap <leader>f <plug>(qf-ls)

nmap <f5> :<c-u>execute 'lmake' <bar> redraw! <bar> lwindow<cr>

nnoremap <esc><esc> <esc>

" https://vim.fandom.com/wiki/Moving_lines_up_or_down
inoremap <a-j> <esc>:m .+1 <cr>gi
nnoremap <a-j>      :m .+1 <cr>
vnoremap <a-j>      :m '>+1<cr>gv
inoremap <a-k> <esc>:m .-2 <cr>gi
nnoremap <a-k>      :m .-2 <cr>
vnoremap <a-k>      :m '<-2<cr>gv

" use internal diff with better diffing algorithm
if has('nvim-0.3.2') || has#('patch-8.1.0360')
    set diffopt=internal,filler,algorithm:histogram,indent-heuristic
endif

" vint: -ProhibitAbbreviationOption
let &history    = max([&hi,  1000])     " entries in cmd and search histories
let &tabpagemax = max([&tpm, 50])       " limit for 'vim -p ...' or ':tab all'
" vint: +ProhibitAbbreviationOption

set lazyredraw                          " don't redraw while executing macros
set sessionoptions-=options             " don't store options across sessions
set shortmess=aoOtT
